Процедура ПоискДокументовПоНомеру(СтрокаПоиска, РезультатПоиска, ДопПарамтры) Экспорт

	ФрагментНомера = Прав(СтрокаПоиска, СтрДлина(СтрокаПоиска) - 1);
	
	//Если в строке поиска только "№", то ничего не делать
	Если ФрагментНомера = "" Тогда
		Возврат;
	КонецЕсли;
	
	//Выбрать документы реализации с номером, содержащим введённую строку
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Реализация.Ссылка КАК Ссылка,
		|	Реализация.Номер КАК Номер,
		|	Реализация.Представление КАК Представление
		|ИЗ
		|	Документ.Реализация КАК Реализация
		|ГДЕ
		|	Реализация.Номер ПОДОБНО &ФрагментНомера";
	
	Запрос.УстановитьПараметр("ФрагментНомера", "%" + ФрагментНомера + "%");
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		
		//Создать элемент результата поиска и добавить его в коллекцию
		ЭлементРезультата = Новый ЭлементРезультатаГлобальногоПоиска(Выборка.Ссылка, СтрНайтиИВыделитьОформлением(Выборка.Представление, ФрагментНомера));
		РезультатПоиска.Добавить(ЭлементРезультата);
		
	КонецЦикла;
	
КонецПроцедуры