
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// Определить, нужно ли обновлять дату в движениях
	ОбновитьДатуДвижений = ЭтоНовый() Или Движения.ОстаткиТоваров.Модифицированность();
	
	Если Не ОбновитьДатуДвижений Тогда
		
		// Проверить, что дата изменилась
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Дата
			|ИЗ
			|	Документ.ВводНачальныхОстатковТоваров
			|ГДЕ
			|	ВводНачальныхОстатковТоваров.Ссылка = &ТекущийДокумент";
		
		Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		ОбновитьДатуДвижений = Выборка.Дата <> Дата;
		
	КонецЕсли;
	
	// Установить всем новую дату, если нужно
	Если ОбновитьДатуДвижений Тогда
		
		Если Не Движения.ОстаткиТоваров.Выбран() Тогда
			Движения.ОстаткиТоваров.Прочитать();
		КонецЕсли;
		
		Для Каждого ЗаписьРегистра Из Движения.ОстаткиТоваров Цикл
			ЗаписьРегистра.Период = Дата;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
