
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.Закупки.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.ПартииТоваров.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТовары.Товар КАК Товар,
		|	ПоступлениеТовары.Товар.ВидТовара КАК ВидТовара,
		|	СУММА(ПоступлениеТовары.Количество) КАК Количество,
		|	МАКСИМУМ(ПоступлениеТовары.Цена) КАК Цена,
		|	СУММА(ПоступлениеТовары.Сумма) КАК Сумма
		|ИЗ
		|	Документ.Поступление.Товары КАК ПоступлениеТовары
		|ГДЕ
		|	ПоступлениеТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТовары.Товар,
		|	ПоступлениеТовары.Товар.ВидТовара";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл

		// регистр Закупки
		Движение = Движения.Закупки.Добавить();
		Движение.Период		= Дата;
		Движение.Поставщик		= Поставщик;
		Движение.Товар			= Выборка.Товар;
		Движение.Количество		= Выборка.Количество;
		Движение.Цена			= Выборка.Цена;
		Движение.Сумма			= Выборка.Сумма;

		// регистр ОстаткиТоваров Приход
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения	= ВидДвиженияНакопления.Приход;
		Движение.Период		= Дата;
		Движение.Контрагент		= Поставщик;
		Движение.Товар			= Выборка.Товар;
		Движение.Количество		= Выборка.Количество;
		Движение.Цена			= Выборка.Цена;
		Движение.Сумма			= Выборка.Сумма;
		
		// регистр ПартииТоваров Приход
		Движение = Движения.ПартииТоваров.Добавить();
		Движение.ВидДвижения	= ВидДвиженияНакопления.Приход;
		Движение.Период		= Дата;
		Движение.Партия			= Ссылка;
		Движение.Товар			= Выборка.Товар;
		Движение.Количество		= Выборка.Количество;
		Движение.ЦенаСписания			= Выборка.Цена;
		Движение.Сумма			= Выборка.Сумма;
		
	КонецЦикла;
	
КонецПроцедуры
