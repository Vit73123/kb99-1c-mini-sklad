
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.Продажи.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТовары.Товар КАК Товар,
		|	РеализацияТовары.Товар.ВидТовара КАК ВидТовара,
		|	СУММА(РеализацияТовары.Количество) КАК Количество,
		|	МАКСИМУМ(РеализацияТовары.Цена) КАК Цена,
		|	СУММА(РеализацияТовары.Сумма) КАК Сумма
		|ИЗ
		|	Документ.Реализация.Товары КАК РеализацияТовары
		|ГДЕ
		|	РеализацияТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТовары.Товар,
		|	РеализацияТовары.Товар.ВидТовара";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл

		// регистр Продажи
		Движение = Движения.Продажи.Добавить();
		Движение.Период		= Дата;
		Движение.Клиент			= Клиент;
		Движение.Товар			= Выборка.Товар;
		Движение.Количество		= Выборка.Количество;
		Движение.Цена			= Выборка.Цена;
		Движение.Сумма			= Выборка.Сумма;

		// регистр ОстаткиТоваров Приход
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения	= ВидДвиженияНакопления.Расход;
		Движение.Период		= Дата;
		Движение.Контрагент		= Клиент;
		Движение.Товар			= Выборка.Товар;
		Движение.Количество		= Выборка.Количество;
		Движение.Цена			= Выборка.Цена;
		Движение.Сумма			= Выборка.Сумма;
		
	КонецЦикла;

	Движения.Записать();
	
	// регистр ПартииТоваров Расход
	Движения.ПартииТоваров.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТовары.Товар КАК Товар,
		|	СУММА(РеализацияТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Документ.Реализация.Товары КАК РеализацияТовары
		|ГДЕ
		|	РеализацияТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТовары.Товар
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Товар
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Товар КАК Товар,
		|	ВТ.Количество КАК Количество,
		|	ПартииТоваровОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ПартииТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ПартииТоваровОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваров.Остатки(
		|				&МоментВремени,
		|				Товар В
		|					(ВЫБРАТЬ
		|						ВТ.Товар КАК Товар
		|					ИЗ
		|						ВТ КАК ВТ)) КАК ПартииТоваровОстатки
		|		ПО ВТ.Товар = ПартииТоваровОстатки.Товар
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПартииТоваровОстатки.Партия.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток),
		|	СУММА(СуммаОстаток)
		|ПО
		|	Товар,
		|	Партия";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаТовар = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаТовар.Следующий() Цикл
	
		КоличествоСписать = ВыборкаТовар.Количество;
		ВыборкаПартия = ВыборкаТовар.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ВыборкаПартия.Следующий() И КоличествоСписать <> 0 Цикл
	
			ВыборкаДетальныеЗаписи = ВыборкаПартия.Выбрать();
	
			КоличествоОстаток = ВыборкаПартия.КоличествоОстаток;
			СуммаОстаток = ВыборкаПартия.СуммаОстаток;

			ЦенаСписания = СуммаОстаток / КоличествоОстаток;

			Количество = Мин(КоличествоСписать, КоличествоОстаток);
			Если Количество = КоличествоОстаток Тогда
				Сумма = СуммаОстаток;
			Иначе
				Сумма = Количество * ЦенаСписания;
			КонецЕсли;

			Движение = Движения.ПартииТоваров.ДобавитьРасход();
			Движение.Товар = ВыборкаТовар.Товар;
			Движение.Период = Дата;
			Движение.Партия = ВыборкаПартия.Партия;
			Движение.Количество = Количество;
			Движение.ЦенаСписания= ЦенаСписания;
			Движение.Сумма = Сумма;
			
			КоличествоСписать = КоличествоСписать -  Количество;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") Тогда
		Клиент = ДанныеЗаполнения.Ссылка;
	КонецЕсли;
	
КонецПроцедуры
